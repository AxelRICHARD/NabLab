/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/

grammar fr.cea.nabla.Nablagen 
with fr.cea.nabla.Nabla 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenModule:
	imports+=Import*
	workflow=Workflow;

Workflow:
	'workflow' name=ID 'transforms' nablaModule=[nabla::NablaModule] 
	'{'
		components+=WorkflowComponent*
	'}';

WorkflowComponent: Nabla2IrComponent | ChildComponent;

ChildComponent: Ir2CodeComponent | Ir2IrComponent;

Ir2IrComponent: TagPersistentVariablesComponent | ReplaceUtfComponent | 
				ReplaceReductionsComponent | OptimizeConnectivitiesComponent |
				FillHLTsComponent;

Nabla2IrComponent:
	'Nabla2Ir' name=ID 
	'{' 
		'timeVariable' '=' timeVar=[nabla::SimpleVar]';'
		'deltatVariable' '=' deltatVar=[nabla::SimpleVar]';'
		'nodeCoordVariable' '=' nodeCoordVar=[nabla::ConnectivityVar]';'
		(dumpIr?='dumpIr' ';')?	
	'}';

TagPersistentVariablesComponent:
	'TagPersistentVariables' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		'dumpedVariables' '=' dumpedVars+=PersistentVar (',' dumpedVars+=PersistentVar)* ';'
		'period' '=' periodValue=[nabla::ArgOrVar] 'for' periodReference=[nabla::ArgOrVar]';'
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

PersistentVar:
	varRef=[nabla::ConnectivityVar]
	'as' varName=STRING;

ReplaceUtfComponent:
	'ReplaceUtf' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

ReplaceReductionsComponent:
	'ReplaceReductions' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(replaceAllReductions?='replaceAllReductions' ';')?
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

OptimizeConnectivitiesComponent:
	'OptimizeConnectivities' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		'connectivities' '=' connectivities+=[nabla::Connectivity] (',' connectivities+=[nabla::Connectivity])* ';'
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

FillHLTsComponent:
	'FillHLTs' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

Ir2CodeComponent:
	'Ir2Code' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		'language' '=' language=Language
		'outputDir' '=' outputDir=STRING ';'
		(disabled?='disabled' ';')?
	'}';

Language: Java | Cpp;

Java: activated?='Java' ';';

Cpp: 'Cpp' '{'
		'maxIterationVariable' '=' maxIterationVar=[nabla::SimpleVar] ';'
		'stopTimeVariable' '=' stopTimeVar=[nabla::SimpleVar] ';'
		'programmingModel' '=' programmingModel=CppProgrammingModel ';'
		('kokkosPath' '=' kokkosPath = STRING ';')? 
		'compiler' '=' compiler=CppCompiler ';'
		('compilerPath' '=' compilerPath = STRING ';')? 
	'}';

enum CppProgrammingModel: Sequential | StlThread | OpenMP | Kokkos | KokkosTeamThread;
enum CppCompiler: GNU | LLVM;
