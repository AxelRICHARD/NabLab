/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/

grammar fr.cea.nabla.Nablagen 
with fr.cea.nabla.Nabla 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenRoot: Application | LibraryProvider;

/***** Application ***********************************************************/

Application:
	(writeIR?='[WriteIr]')?
	'Application' name=ID ';'
	mainModule = MainModule
	additionalModules += AdditionalModule*
	(vtkOutput = VtkOutput)?
	(levelDB = LevelDB)?
	targets += Target*;

NablagenModule: MainModule | AdditionalModule;

MainModule:
	'MainModule' type=[nabla::NablaModule] name=ID
	'{'
		'meshClassName' '=' meshClassName=STRING ';'
		'nodeCoord' '=' nodeCoord=[nabla::ConnectivityVar]';'
		'time' '=' time=[nabla::SimpleVar]';'
		'timeStep' '=' timeStep=[nabla::SimpleVar]';'
		// The two following variables are just needed for the C++ display (progression bar)
		('iterationMax' '=' iterationMax=[nabla::SimpleVar] ';'
		'timeMax' '=' timeMax=[nabla::SimpleVar] ';')?
	'}';

AdditionalModule:
	'AdditionalModule' type=[nabla::NablaModule] name=ID
	'{'
		varLinks += VarLink*
	'}';

VarLink:
	additionalModule=[AdditionalModule] '.' additionalVariable=[nabla::Var] '=' mainModule=[MainModule] '.' mainVariable=[nabla::Var] ';';

VtkOutput:
	'VtkOutput'
	'{'
		'periodReferenceVariable' '=' periodReferenceModule=[MainModule] '.'periodReferenceVar=[nabla::ArgOrVar] ';'
		'outputVariables' '=' vars += OutputVar (',' vars += OutputVar)* ';'
	'}';

OutputVar:
	moduleRef=[NablagenModule] '.' varRef=[nabla::ConnectivityVar] 'as' varName=STRING;

LevelDB:
	'LevelDB'
	'{'
		'levelDBPath' '=' levelDBPath = STRING ';'
	'}';

Target: type=TargetType
'{'
	'outputPath' '=' outputDir=STRING ';'
	variables+=TargetVar*
'}';

TargetVar:
	key=ID '=' value=STRING ';';

enum TargetType: Java | CppSequential | StlThread | OpenMP | Kokkos | KokkosTeamThread;


/****** Library Provider *****************************************************/

LibraryProvider: 'Provider' name=ID ':' library=[nabla::NablaModule]
'{'
	'targets' '=' targets+=TargetType (',' targets+=TargetType)* ';'
	'facadeClass' '=' facadeClass = STRING ';'
	'libHome' '=' libHome=STRING ';' // includes in libHome/includes, libs in libHome/libs
	'libName' '=' libName=ID ';'
'}';

