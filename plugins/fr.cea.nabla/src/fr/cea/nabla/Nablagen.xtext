/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/

grammar fr.cea.nabla.Nablagen 
with fr.cea.nabla.Nabla 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenModule:
	imports+=Import*
	config=NablagenConfig;

NablagenConfig:
	'nablagen' 'for' nablaModule=[nabla::NablaModule] 
	'{'
		'time' '=' timeVar=[nabla::SimpleVar]';'
		'timeStep' '=' deltatVar=[nabla::SimpleVar]';'
		'nodeCoord' '=' nodeCoordVar=[nabla::ConnectivityVar]';'
		(vtkOutput = VtkOutput)?
		targets += Target*
	'}';

VtkOutput:
	'output' 'reference' periodReference=[nabla::ArgOrVar]
	'{'
		vars += OutputVar*
	'}';

OutputVar:
	varRef=[nabla::ConnectivityVar] 'as' varName=STRING ';';

Target: Java | Cpp;

Java: 'java' 'in' outputDir=STRING ';';
Cpp: 'cpp' 'in' outputDir=STRING
	'{'
		'iterationMax' '=' maxIterationVar=[nabla::SimpleVar] ';'
		'timeMax' '=' stopTimeVar=[nabla::SimpleVar] ';'
		'programmingModel' '=' programmingModel=CppProgrammingModel ';'
		('kokkosPath' '=' kokkosPath = STRING ';')? 
		'compiler' '=' compiler=CppCompiler ';'
		('compilerPath' '=' compilerPath = STRING ';')? 
	'}';

enum CppProgrammingModel: Sequential | StlThread | OpenMP | Kokkos | KokkosTeamThread;
enum CppCompiler: GNU | LLVM;
