/*******************************************************************************
 * Copyright (c) 2018 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 * 	Benoit Lelandais - initial implementation
 * 	Marie-Pierre Oudot - initial implementation
 * 	Jean-Sylvain Camier - Nabla generation support
 *******************************************************************************/
grammar fr.cea.nabla.Nabla with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate nabla "http://www.cea.fr/nabla/Nabla"

NablaModule:
	'module' name=QualifiedName ';'
	imports+=Import*
	('items' '{' items+=ItemType (',' items+=ItemType)* '}'
	'connectivities' '{' connectivities+=Connectivity* '}')?
	('functions' '{' functions+=(Function | Reduction)* '}')?
	variables+=(ScalarVarDefinition | VarGroupDeclaration)*
	jobs+=Job*;

Import: 'with' importedNamespace=QualifiedNameWithWildcard ';';

/* ITEMS *************************************************/
ItemType:
	name=ID;

/* JOBS **************************************************/
Job: name=ID ':' instruction=Instruction;

/* INSTRUCTIONS ******************************************/
Instruction: 
	ScalarVarDefinition |
	VarGroupDeclaration | // only scalar variables (ScalarVar type)
	{InstructionBlock} '{' instructions+=Instruction+ '}' |
	{Affectation} varRef=VarRef '=' expression=Expression ';' |
	{Loop} '\u2200' range=RangeSpaceIterator (',' singletons+=SingletonSpaceIterator)* ',' body=Instruction |
	{If} 'if' '(' condition=Expression ')' then=Instruction (=>'else' else=Instruction)?;

/* ITERATEURS ********************************************/
SpaceIterator: RangeSpaceIterator | SingletonSpaceIterator;
RangeSpaceIterator: name=ID '\u2208' container=ConnectivityCall; // must return a collection
SingletonSpaceIterator: name=ID '=' container=ConnectivityCall; // must return a singleton
ConnectivityCall: connectivity=[Connectivity]  '('(args+=SpaceIteratorRef (',' args+=SpaceIteratorRef)*)?')';
SpaceIteratorRef: target=[SpaceIterator] (('+' inc=INT) | ('-' dec=INT))?; // inc/dec only valid on a range

/* VARIABLES ********************************************/
ScalarVarDefinition: (const?='const')? type=BaseType variable=SimpleVar '=' defaultValue=InitializationExpression ';';
VarGroupDeclaration: type=BaseType variables+=Var (',' variables+=Var)* ';';
Var: SimpleVar | ConnectivityVar;
SimpleVar: name=ID;
ConnectivityVar: name=ID '{' dimensions+=[Connectivity] (',' dimensions+=[Connectivity])* '}';

/* CONNECTIVITES *****************************************/
Connectivity: name=ID ':' (inTypes+=[ItemType] ('\u00D7' inTypes+=[ItemType])*)?  '\u2192' returnType=ItemArgType ';';
ItemArgType: multiple?='{' type=[ItemType] '}' | type=[ItemType];

/* FONCTIONS / REDUCTIONS ********************************/
Function: name=ID ':' argGroups+=FunctionArg (',' argGroups+=FunctionArg)* ';';
FunctionArg: (dimVars+=DimensionVar (',' dimVars+=DimensionVar)* '|')? (inTypes+=ArgType ('\u00D7' inTypes+=ArgType)*)? '\u2192' returnType=ArgType;
Reduction: name=ID ':' argGroups+=ReductionArg (',' argGroups+=ReductionArg)* ';';
ReductionArg:  (dimVars+=DimensionVar (',' dimVars+=DimensionVar)* '|')? '(' seed=Expression ',' collectionType=ArgType ')' '\u2192' returnType=ArgType;

ArgType: root=PrimitiveType ('[' dimensions+=Dimension (',' dimensions+=Dimension)* ']')?;
DimensionVar: name=ID;
Dimension: DimensionOperation;
DimensionOperation returns Dimension:
	DimensionAtomic ({DimensionOperation.left=current} op=('*'|'+') right=DimensionAtomic)*;
DimensionAtomic returns Dimension: 
	{DimensionInt} value=INT |
	{DimensionVarReference} target=[DimensionVar];

/* EXPRESSIONS ******************************************/
Expression: ContractedIf;

ContractedIf returns Expression:
	Or ({ContractedIf.condition=current} '?' then=Or ':' else=Or)*;

Or returns Expression: // on garde l'attribut op pour la validation
	And ({Or.left=current} op='||' right=And)*;

And returns Expression: // on garde l'attribut op pour la validation
	Equality ({And.left=current} op='&&' right=Equality)*;

Equality returns Expression:
	Comparison ({Equality.left=current} op=('=='|'!=') right=Comparison)*;

Comparison returns Expression:
	PlusOrMinus ({Comparison.left=current} op=('>='|'<='|'>'|'<') right=PlusOrMinus)*;

PlusOrMinus returns Expression: // on garde l'attribut op pour la validation
	MulOrDiv ( ({Plus.left=current} op='+' | {Minus.left=current} op='-') right=MulOrDiv)*;

MulOrDiv returns Expression:
	Modulo ({MulOrDiv.left=current} op=('*'|'/') right=Modulo)*;

Modulo returns Expression:
	Primary ({Modulo.left=current} op=('%') right=Primary)*;

Primary returns Expression:
	{Parenthesis} '(' expression=Expression ')' | 
	{UnaryMinus} '-' expression=Primary | 
	{Not} '!' expression=Primary | 
	Atomic;

Atomic returns Expression:
	{IntConstant} value=INT |
	{RealConstant} value=REAL |
	{BoolConstant} (value?='true' | 'false') |
	{MinConstant} type=PrimitiveType '.MinValue' |
	{MaxConstant} type=PrimitiveType '.MaxValue'|
	{FunctionCall} function=[Function|QualifiedName] '(' (args+=Expression (',' args+=Expression)*)? ')' | 
	{ReductionCall} reduction=[Reduction|QualifiedName] '{' range=RangeSpaceIterator (',' singletons+=SingletonSpaceIterator)* '}' '(' arg=Expression ')' |
	VarRef;

VarRef: variable=[Var] 
	('^{' timeIterator=TimeIterator '}')?
	('{' spaceIterators+=SpaceIteratorRef (',' spaceIterators+=SpaceIteratorRef)* '}')? 
	('[' indices+=INT (',' indices+=INT)? ']')?;

InitializationExpression:
	Expression |
	RealVectorConstant |
	{RealMatrixConstant} '[' values+=RealVectorConstant (',' values+=RealVectorConstant)+ ']' |
	{BaseTypeConstant} type=BaseType '(' value=Expression ')'; // Expression Type is Real because there are only Real arrays for the moment but it will become expression

RealVectorConstant: '[' values+=REAL (',' values+=REAL)* ']';

TimeIterator: 
	{InitTimeIterator} 'n=0' | 
	{NextTimeIterator} 'n+1' (hasDiv?='/' div=INT)?;

/* TYPES *************************************************/
BaseType: root=PrimitiveType ('[' sizes+=INT (',' sizes+=INT)? ']')?;
enum PrimitiveType: Int='\u2115' | Real='\u211D' | Bool='\u213E';

/* DATA TYPE *********************************************/
QualifiedName: ID ('.' ID)*;
QualifiedNameWithWildcard: QualifiedName '.*'?;


/* TERMINAUX *********************************************/
@Override 
terminal ID: '^'? ('a'..'z'|'A'..'Z'|'\u0394'..'\u03F2'|'\u220F'..'\u221A'|'_') ('a'..'z'|'A'..'Z'|'\u0394'..'\u03F2'|'0'..'9'|'\u221A'|'_')*;
terminal REAL returns ecore::EDouble: ('0'..'9')+ '.' ('0'..'9')+ ('e'('+'|'-')('0'..'9')+)?;
//terminal DIM returns ecore::EChar: '\u00B2'..'\u00B3' | '\u2074'..'\u2079';
