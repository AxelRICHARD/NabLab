/*******************************************************************************
 * Copyright (c) 2018 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 * 	Benoit Lelandais - initial implementation
 * 	Marie-Pierre Oudot - initial implementation
 * 	Jean-Sylvain Camier - Nabla generation support
 *******************************************************************************/
grammar fr.cea.nabla.Nablagen 
with fr.cea.nabla.Nabla 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenModule:
	imports+=Import*
	workflow=Workflow;

Workflow:
	'workflow' name=ID 'transforms' nablaModule=[nabla::NablaModule] 
	'{'
		components+=WorkflowComponent*
	'}';
	
WorkflowComponent: Nabla2IrComponent | ChildComponent;

ChildComponent: Ir2CodeComponent | Ir2IRComponent;
					
Ir2CodeComponent: Ir2JavaComponent | Ir2KokkosComponent;

Ir2IRComponent: TagPersistentVariablesComponent | ReplaceUtfComponent | 
				ReplaceInternalReductionsComponent | OptimizeConnectivitiesComponent |
				FillHLTsComponent;
					
IrWriterComponent: Ir2IRComponent | Nabla2IrComponent;

Nabla2IrComponent:
	'Nabla2Ir' name=ID 
	'{' '}';
	
TagPersistentVariablesComponent:
	'TagPersistentVariables' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		'variables' '=' vars+=PersistentVar (',' vars+=PersistentVar)* ';'
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';
	
PersistentVar:
	varRef=[nabla::ArrayVar]
	'as' varName=STRING;

ReplaceUtfComponent:
	'ReplaceUtf' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')? 
	'}';
	
ReplaceInternalReductionsComponent:
	'ReplaceInternalReductions' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')? 
	'}';

OptimizeConnectivitiesComponent:
	'OptimizeConnectivities' name=ID 'follows' parent=[WorkflowComponent] 
	'{' 
		'connectivities' '=' connectivities+=[nabla::Connectivity] (',' connectivities+=[nabla::Connectivity])* ';'
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')?
	'}';

FillHLTsComponent:
	'FillHLTs' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(disabled?='disabled' ';')?
		(dumpIr?='dumpIr' ';')? 
	'}';
	
Ir2JavaComponent:
	'Ir2Java' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(disabled?='disabled' ';')?
		'outputDir' '=' outputDir=STRING ';' 
	'}';

Ir2KokkosComponent:
	'Ir2Kokkos' name=ID 'follows' parent=[WorkflowComponent]
	'{' 
		(disabled?='disabled' ';')?
		'outputDir' '=' outputDir=STRING ';' 
	'}';